group 'ru.demkin'
version '1.0.0-SNAPSHOT'

subprojects {
  apply plugin: 'java'
  apply plugin: 'application'
  apply plugin: 'findbugs'
  apply plugin: 'checkstyle'

  repositories {
    mavenCentral()
    jcenter()
  }
  sourceCompatibility = 1.8
  dependencies {
    testCompile 'org.testng:testng:6.9.10'
  }
  version = '1.0'

  //Findbugs settings
  tasks.withType(FindBugs) {
    reports {
      xml.enabled false
      html.enabled true
    }
  }
  findbugs {
    excludeFilter = file("$rootProject.projectDir/config/findbugs/findbugs-filter.xml")
  }

  //CheckStyle settings
  tasks.withType(Checkstyle) {
    reports {
      xml.enabled false
      html.enabled true
    }
  }
  checkstyle {
    toolVersion = '6.11'
    configFile = file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
  }

  //Test settings
  test {
    useTestNG()
  }

  //Verification settings
  task('verify') {
    doLast {
      println 'Verification completed'
    }
  }
  verify.dependsOn checkstyleMain
  checkstyleMain.dependsOn findbugsMain
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}
